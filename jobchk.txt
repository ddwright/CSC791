/*
   REXX
   THIS PROGRAM IS USED TO MONITOR JOBS ON SDSF
*/

MAIN_PROCESS:

    /*
       GET THE PARMS AND PARSE THEM OUT
       ADD MORE ROBUST PARM CHECKING IN
       THE FUTURE
    */
    RUSERID = TRANSLATE(WORD(ARG(1),1))
    RTIME   = TRANSLATE(WORD(ARG(1),2))
    RTYPE   = TRANSLATE(WORD(ARG(1),3))

    IF RUSERID = "-H" THEN
       CALL DISPLAY_OPTIONS

    CALL CREATE_LOGFILE

    SAY 'DISPLAYING' RTYPE 'JOBS FOR' RUSERID 'FOR' RTIME 'SECONDS'

    /* ESTABLISH THE SDSF ENVIRONMENT */
    RC=ISFCALLS('ON')
    IF RC <> 0 THEN
      EXIT RC

    /* SET THE USERID TO WHAT WAS PASSED */
    ISFPREFIX = RUSERID

    /*
       LOOP THROUGH UNTIL THE ELAPSED TIME EQUALS THE PARM
       BUILD IN A DELAY SO ITS NOT CONSTANTLY CHECKING
    */
    TTIME = TIME('E')
    DO UNTIL TIME('E') > RTIME
        DO UNTIL TIME('E') >= TTIME
        END
        CALL CHECK_USERS
        TTIME = TIME('E') + 2
    END


    SAY 'EXECUTION COMPLETE'

    RC=ISFCALLS('OFF')
    "FREE F(LOGOUT)"

RETURN

CHECK_USERS:

    ADDRESS SDSF "ISFEXEC DA"
    IF RC <> 0 THEN
      EXIT RC

    /* GET FIXED FIELD NAME FROM FIRST WORD */
    /* OF ISFCOLS SPECIAL VARIABLE          */
    FIXEDFIELD = WORD(ISFCOLS,1)

    IF ISFROWS == 0 THEN
        SAY ISFPREFIX 'NOT SIGNED ON.'

    DO IX=1 TO ISFROWS
    /*    SAY "NOW PROCESSING JOB:" VALUE(FIXEDFIELD"."IX) */

      SELECT
          WHEN JTYPE.IX = RTYPE THEN
              SAY TIME() OWNERID.IX "IS SIGNED ON AND HAS USED" CPU.IX
          WHEN RTYPE == "" THEN
              SAY TIME() OWNERID.IX "IS EXECUTING AND HAS USED" CPU.IX
      END

      PSTRING.IX = ""
      DO JX=1 TO WORDS(ISFCOLS)
          COL = WORD(ISFCOLS,JX)
          COLVALUE.JX = COL VALUE(COL"."IX)

          IF JX=1 THEN
              PSTRING.IX = VALUE(COL"."IX)
          ELSE
              PSTRING.IX = PSTRING.IX || "," || VALUE(COL"."IX)

          IF COLVALUE.JX = "" THEN
              COLVALUE.JX = " "
      END

      "EXECIO * DISKW LOGOUT (STEM PSTRING."

   END

RETURN

CREATE_LOGFILE:

    LOGFILE="DHS9172.JOBCHK.CSV"

    ADDRESS TSO
    STATUS = SYSDSN("'"LOGFILE"'")

    IF STATUS = "OK" THEN
    DO
        SAY "DELETING LOG FILE :" LOGFILE
        "DELETE '"LOGFILE"'"
    END

    "ALLOC FI(LOGOUT) DA('"LOGFILE"') SHR NEW SPACE(50,20)
     DSORG(PS) RECFM(F,B) LRECL(400) BLKSIZE(27600)"

RETURN

DISPLAY_OPTIONS:

    SAY '-H HELP'
    SAY 'USER-MASK {RUN-TIME TYPE-OF-JOB}'
    EXIT 0

RETURN
